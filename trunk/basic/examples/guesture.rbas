	'-----------------------------------------------------
	' Gesture recognition
	'-----------------------------------------------------

	'define some matrices
	z=32

	!MAT DEF B;z;z  'image being processed
	!MAT DEF A;z;z   'temp image for load
	!MAT DEF C;z;z   'background
	!MAT DEF T;z;z  'temp image for masking


' 	test code
if t=1 then
	!IMAGE LOAD z;"back.jpg"
	LIST C=@!
	!IMAGE LOAD z;"object.jpg"
	LIST B=@!

	a=`p
        goto processimg
endif

	print "Get static background image"
	f=0
	gosub camgrab

	list c=@b
	'print "Background"
	'!mat print c

	'-----------------------------------------------------
	' main loop
	'-----------------------------------------------------

mainloop:
        print "ready?"
        a=$kbd

	f=1
	gosub camgrab

processimg:

	SCALE @c-@b,15,1,0
	LIST b=@!

        out @{4,27,`[,`2,`J}  'clear screen

	if a=`p then
            print @B:z
        endif

	!MAT HIST 2;b  'Horizontal Intensity Histo

	'-----------------------------------------------------
	' find torso
	'-----------------------------------------------------

	L=0
	M=0
	R=0
	S=0
	P=3  'threshold arm
	Q=10 'threshol body

	FOR I=0 TO 31
		IF (@![i]>=P) and (L=0) THEN
		    L=i
		ENDIF
		IF (@![i]>=Q) and (m=0) THEN
		    m=i
		ENDIF
		IF (@![i]<Q) and (M>0) and (s=0) THEN
		    S=i
		ENDIF
		IF (@![i]<P) and (S>0) and (r=0) THEN
		    R=i
		ENDIF
	NEXT I

	W=S-M
	L=M-L
	R=R-S
	N=S

	PRINT "Torso Width=";W
	PRINT "L hand=     ";L
	PRINT "R hand=     ";R

        gosub initarm

	'-----------------------------------------------------
	' find left hand position
	'-----------------------------------------------------

	LIST T=@b
	!MAT ZERO T;M;0;31;31    'mask off right-hand side

	gosub processarm

	if k=1 then
		PRINT "left arm straight"
		gosub larmout
	endif

	if k=2 then
		PRINT "left arm up"
		gosub larmup
	endif

	if k=3 then
		PRINT "left arm down"
		gosub rarmdown
	endif

	'-----------------------------------------------------
	' find right arm position
	'-----------------------------------------------------
rhs:
	LIST T=@B
	!MAT ZERO T;0;0;N;31   	' mask off left-hand side

	gosub processarm

	if k=1 then
		PRINT "right arm straight"
		gosub rarmout
	endif

	if k=2 then
		PRINT "right arm up"
		gosub rarmup
	endif

	if k=3 then
		PRINT "right arm down"
		gosub rarmdown
	endif

mvarm:
        gosub movearm
	if t<>1 then mainloop
fin:
	end

'-----------------------------------------------------

processarm:
	'PRINT @T:32
	!MAT HIST 1;T  'Vertical Intensity Histo
	'PRINT @!

	k=0

	M=0
	S=0
	P=1 'threshold arm

	FOR I=0 TO 31
		IF (@![i]>=P) and (M=0) THEN
		    M=i
		ENDIF
		IF (@![i]<P) and (s=0) and (m>0) THEN
		    S=i
		ENDIF
	NEXT I

	P=$MAX(@!)

        if p=0 then
           	print "nothing detected"
		return
        endif

	PRINT "Width=   ";S-M
	PRINT "Max=     ";

	IF (s-m)/p > 1 THEN
		k=1
	ELSE
		'diagonal arms 
		p=$IMAX(@!);l
		PRINT p
		IF P<10 THEN
			k=2
		ELSE
			k=3
		ENDIF
	ENDIF 

	return

'-----------------------------------------------------
' load webcam (jpg) image
'-----------------------------------------------------
' F=0 return when image is static i.e. amount of change lt threshold
' F=1 return when image differs i.e. gt than threshold
'-----------------------------------------------------

camgrab:
	!wait image
	!image load z
	LIST A=@!            'initialise prev image
loop:
	!wait image
	!image load z      'load next image
	LIST B=@! 
	S=$abs($sum(@B-@A))  'compare with prev & next images
	print S
	if ((f=1) and (s>60)) or  ( (f=0) and (s<60)) then done    'if difference > threshold done
	LIST A=@B            'else prev = next
	goto loop            'go round again
done:
	return

'-----------------------------------------------------
'arm movements - based on 'dance hands' configuration
'-----------------------------------------------------
initarm:
    LIST v=16,141,86,248,41,103,103,161,0,206,144, 93, 95, 65,155,156,183
    return

movearm:
   print @v
   move @v,10,1000
   return

larmdown:
   set @v,11,95   '-1
   set @v,12,109  '+45
   return

larmout:
   set @v,11,142   '+47
   set @v,12,109   '+45
   return

larmup:
   set @v,11,195   '+100
   set @v,12,93    '+28
   return

rarmdown:
   set @v,14,157   '1
   set @v,15,138   '-45
   return

rarmout:
   set @v,14,110   '-47
   set @v,15,138   '-45
   return

rarmup:
   set @v,14,56    '-100
   set @v,15,155   '-28
   return

