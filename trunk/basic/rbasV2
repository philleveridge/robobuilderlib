#!/bin/bash

echo "Basic Pre-Processor V2 - $1"

if [[ `uname -a | cut -d' ' -f2` == "OpenWrt" ]]
then
BASIC=rbasic
else
BASIC="/home/phil/Desktop/R2/svn src/robobuilderlib/basic/Basic"
fi


awk 'BEGIN {ln=10; prog=""; ifc=0;}
{
	gsub(/\015$/,"");
	gsub(/['\''].*$/,"");
	gsub(/^ +/,"");
	gsub(/ +$/,"");
	if ($0=="") next;

	p = gensub(/[Cc]all[ ]+([A-Za-z])[ ]*=[ ]*(.+)[ ]*\((.+)\)/,"List !=^0,\\3\nGosub \\2\nList !=#\\1", "G");
	$0=p;

	p = gensub(/[Cc]all[ ]+(.+)[ ]*\((.+)\)/,"List !=^\\2\nGosub \\1\n", "G");
	$0=p;

	prog = prog $0 "\n";
}
END {
	IGNORECASE=1
	prog = gensub(/\<endfunction\>/, "\003endfunction","G", prog);
	prog = gensub(/\<endproc\>/, "\003endproc", "G", prog);
	IGNORECASE=0

        for (c=0; c<3; c++)
	{
		prog = gensub(/[Ll]ocal[ ]+([A-Za-z])([^\003]+)\003endfunction/,
			"List !=^0 \nList !=:\\1\n\\2List !=#\\1\n\003endfunction", "G", prog);

		prog = gensub(/[Ll]ocal[ ]+([A-Za-z])([^\003]+)\003endproc/,
			"List !=^0 \nList !=:\\1\n\\2List !=#\\1\n\003endproc", "G", prog);
	}

	prog = gensub(/[Ff]unction[ ]+([A-Za-z])[ ]+:[ ]+([A-Za-z0-9]+)[ ]*\(([^)]+)\)([^\003]+)\003endfunction/,
		"\\2:\nLIST !=:\\1\\3\n\\4\nLIST !=#\\3\nLIST !=:\\1\nReturn\n", "G", prog);

	prog = gensub(/[Pp]rocedure[ ]+([A-Za-z0-9]+)[ ]*\(([^)]+)\)([^\003]+)\003endproc/,
		"\\1:\nLIST !=:\\2\n\\3List !=#\\2\nReturn \n", "G", prog);

	print prog;

}' "$1"   > bas1.txt

cat bas1.txt

awk 'BEGIN {ln=10; prog=""; ifc=0; IGNORECASE=1;}
{
	gsub(/['\''].*$/,"");

	if (/^[A-Za-z0-9]+:/)
	{
		sym[substr($1,1,length($1)-1)]=ln;
		gsub(/^[A-Za-z0-9]+:/,"");	
	}

	gsub(/^ +/,"");
	gsub(/ +$/,"");


	gsub(/\<network\>/,"networ");

	if (/^const/)
	{
		sym[$2]=$3;
		next;
	}

	if (/^if.*then$/)
	{
	   ifc+=1;
	   gsub(/if/,"IF");
	   gsub(/then/,"THEN " ln+10 " ELSE-00" ifc);
	}

	if (/^else$/)
	{
	   prog = prog ln  " GOTO-00" ifc "\n";
	   ln += 10;
	   gsub("ELSE-00" ifc, "ELSE " ln, prog);

	   next;
	}

	if (/^endif$/)
	{
	   gsub("ELSE-00" ifc, "ELSE " ln, prog);
	   gsub("GOTO-00" ifc, "GOTO " ln, prog);
	   ifc-=1;
	   next;
	}


	if ($0=="") next; #{prog = prog "\n"; next;}

	if (/^[A-Za-z][ ]*=/)
	{
	 	$0 = "LET " $0;
	}

	if (/^[^0-9]/) {
		prog = prog ln  " "  $0 "\n";
        	ln=ln+10;
	}
	else
	{
		#print $0;
		prog =  prog $0 "\n";
	}
}
END {
	IGNORECASE=0;
    	print "Sym table" > "sym.txt"; 
	for (i in sym) { 
    	printf "%10s %10s\n", i, sym[i] > "sym.txt"; 
    	gsub("\\<" i "\\>" ,sym[i],prog);
	}

	
	print prog
	print "."
}' <bas1.txt >bas.txt

cat sym.txt
cat bas.txt

if [[ "$2" == "-C" ]]
then
"$BASIC" COMPILE <bas.txt
fi


echo "Done"





